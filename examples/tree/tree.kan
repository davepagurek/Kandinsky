define branch as
  rectangle: {x: -2, y: -50, width: 4, height: 52}.

define leaf as
  circle: {
    radius: random: { from: 15, to: 40 },
    fill: "#F7BED9"
  };
  shifted: {up: 15}.

define tree as
  grouped: {drawings: repeated: {
    times: 3,
    each:
      given: {
        that: lt: {lhs: random, rhs: 0.5},
        then:
          tree;
          shifted: {up: 50};
          with: {
            drawing: branch
          };
          stretched: {x: 0.8, y: 0.75},
       else: given: {
        that: lt: {lhs: random, rhs: 0.4},
        then: leaf
       }
      };
      rotated: {angle: random: {from: -50, to: 50}}
  }};
  depth_limited: {call: "tree", max: 4};
  or: {drawing: leaf}.

define result as
  branch;
  with: {drawing:
    tree;
    shifted: {up: 50}
  };
  shifted: {down: 400, right: 400}.
